#+TITLE: Doom Emacs Config
#+DATE: <2025-04-06 Sun>
#+hugo_section: docs/0_meta/0b_system_initialization

This is my personal =config.el= for Doom Emacs. It controls font settings, theme, Org-mode behavior, tangling and exporting, Copilot and AI tooling, and various Doom-specific enhancements.

We set lexical binding for the file.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
;;; $DOOMDIR/config.el -*- lexical-binding: t; -*-
#+end_src

We begin by setting the primary font for Doom. I'm using =Maple Mono= at size 20, with increased spacing to give it a clean and open feel.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(setq doom-font (font-spec :family "Maple Mono" :size 20 :spacing 90))
#+end_src

The theme I use is =catppuccin= — soft, readable, and comfortable on the eyes.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(setq doom-theme 'catppuccin)
#+end_src

I enable relative line numbers across the board, which helps with navigation.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(setq display-line-numbers-type 'relative)
#+end_src

My Org files live in the root of my repository — this ensures that tangling and exporting behave predictably from anywhere.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(setq org-directory "~/nickseagull.dev/")
(setq user-full-name "Nikita Tchayka")
#+end_src

Now we define a helper that determines whether an Org file contains any tangleable code blocks.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(defun ns/org-file-has-tangle-blocks-p (file)
  "Return non-nil if FILE contains any Babel src block with a :tangle path or yes."
  (with-temp-buffer
    (insert-file-contents file)
    (goto-char (point-min))
    (re-search-forward "#\\+BEGIN_SRC.*:tangle\\s-+\\([^ \t\n]+\\)" nil t)))
#+end_src

We now define a function that automatically tangles an Org file when it is saved, but only if it includes tangleable blocks.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(defun ns/org-babel-auto-tangle ()
  "Auto-tangle current Org file on save if it contains any tangleable blocks."
  (when (and (buffer-file-name)
             (string= (file-name-extension (buffer-file-name)) "org")
             (ns/org-file-has-tangle-blocks-p (buffer-file-name)))
    (org-babel-tangle)))
#+end_src

This behavior is attached to =org-mode= buffers only.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(after! org-mode
  (add-hook! org-mode
    (lambda ()
      (add-hook! after-save-hook #'ns/org-babel-auto-tangle nil t))))
#+end_src

Now we define a recursive tangler for all Org files in a directory.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(defun ns/tangle-org-files-in-dir (directory)
  "Recursively tangle Org files in DIRECTORY that contain Babel blocks with
:tangle."
  (interactive "DDirectory: ")
  (let ((org-files (directory-files-recursively directory "\\.org$")))
    (dolist (file org-files)
      (when (ns/org-file-has-tangle-blocks-p file)
        (with-current-buffer (find-file-noselect file)
          (org-babel-tangle))))))
#+end_src

To complement tangling, we now define a function that exports all Org files under a directory to Hugo-compatible Markdown files, preserving their relative structure.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(defun ns/hugo-export-org-files-in-dir (org-root-dir hugo-root-dir)
  "Recursively export Org files under ORG-ROOT-DIR using ox-hugo.
Place the resulting .md files in HUGO-ROOT-DIR/content/docs/,
preserving the same subdirectory structure.

Skips killing the current buffer if it's one of the exported files."
  (interactive "DOrg root directory: \nDHugo root directory: ")
  (require 'ox-hugo)
  (let ((org-files (directory-files-recursively org-root-dir "\\.org$"))
        (content-root (expand-file-name "content/docs/" hugo-root-dir))
        (current (current-buffer)))
    (dolist (org-file org-files)
      (let* ((relative-path (file-relative-name org-file org-root-dir))
             (md-filename (concat (file-name-sans-extension relative-path) ".md"))
             (final-md-path (expand-file-name md-filename content-root))
             (target-dir (file-name-directory final-md-path)))
        (make-directory target-dir t)
        (with-current-buffer (find-file-noselect org-file)
          (setq-local org-hugo-base-dir hugo-root-dir)
          (org-hugo-export-wim-to-md)
          (unless (eq (current-buffer) current)
            (kill-buffer))))))
  (message "Finished exporting Org files from %s to %s/content/docs"
           (abbreviate-file-name org-root-dir)
           (abbreviate-file-name hugo-root-dir)))
#+end_src

This next function ties it all together: tangle, then export everything. This is the one I call when building the site.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(defun ns/tangle-and-export-all-org-files ()
  "Tangle and export all Org files in `org-directory` to Hugo markdown.
Hugo project is assumed to be at `org-directory/website/src/`."
  (interactive)
  (let* ((org-root-dir (expand-file-name org-directory))
         (hugo-root-dir (expand-file-name ".webgen/src/" org-root-dir)))
    (ns/tangle-org-files-in-dir org-root-dir)
    (ns/hugo-export-org-files-in-dir org-root-dir hugo-root-dir)))
#+end_src

We also hook this function to saving files, but only if they belong to the Org source tree.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(defun ns/auto-tangle-and-export-on-save ()
  "Auto tangle and export if the saved Org file is under `org-directory`."
  (when (and (eq major-mode 'org-mode)
             (string-prefix-p (expand-file-name org-directory)
                              (buffer-file-name)))
    (run-with-idle-timer 0.5 nil #'ns/tangle-and-export-all-org-files)))

(add-hook 'after-save-hook #'ns/auto-tangle-and-export-on-save)
#+end_src

I use GitHub Copilot for completions, and fall back to Company if needed.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(use-package! copilot
  :hook (prog-mode . copilot-mode)
  :bind (:map copilot-completion-map
              ("<tab>" . 'copilot-accept-completion)
              ("TAB" . 'copilot-accept-completion)
              ("C-TAB" . 'copilot-accept-completion-by-word)
              ("C-<tab>" . 'copilot-accept-completion-by-word)))
#+end_src

I also reposition Treemacs to the right of the screen.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(after! treemacs
  (setq treemacs-position 'right))
#+end_src

The =aidermacs= package gives me AI-driven tools and workflows. I bind its main menu to =C-c a=.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(use-package! aidermacs
  :bind
  (("C-c a" . aidermacs-transient-menu))
  :custom
  (aidermacs-use-architect-mode t)
  (aidermacs-default-model "gpt-4o"))
#+end_src

For Go development, I bind the DAP (debugging) hydra to =SPC m D=.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(add-hook! go-mode
  (map! :localleader
        :map go-mode-map
        "D" #'dap-hydra))
#+end_src

Lastly, I enable =clipetty= so that Emacs can use the system clipboard over SSH.

#+begin_src emacs-lisp :tangle ../../.hm/emacs/doom.d/config.el
(setq dap-auto-configure-features '(sessions locals controls tooltip))

(use-package! clipetty
  :hook (after-init . global-clipetty-mode))
#+end_src
